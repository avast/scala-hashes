buildscript {
    repositories {
        jcenter()
        dependencies {
            classpath 'com.github.ben-manes:gradle-versions-plugin:0.38.0'
        }
    }
}

plugins {
    id 'signing'
    id('io.github.gradle-nexus.publish-plugin') version '1.0.0'
}

apply plugin: 'idea'
apply plugin: 'scala'
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'com.github.ben-manes.versions'

sourceCompatibility = JavaVersion.VERSION_1_8

ext {
    circeVersion = '0.13.0'
}

group = 'com.avast.hashes'
archivesBaseName = "scala-hashes_$scalaVersion"

repositories {
    mavenCentral()
}

dependencies {
    compile "org.scala-lang:scala-library:${scalaLibraryVersion}"
    compileOnly "org.json4s:json4s-core_${scalaVersion}:3.6.9"
    compileOnly "io.circe:circe-core_${scalaVersion}:${circeVersion}"

    testCompile "org.scalatest:scalatest_${scalaVersion}:3.2.0"
    testCompile "org.scalatestplus:junit-4-12_${scalaVersion}:3.1.3.0"
    testCompile "io.circe:circe-core_${scalaVersion}:${circeVersion}"
    testCompile "junit:junit:4.13.2"
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            artifactId = archivesBaseName
            pom {
                name = 'Scala Hashes'
                description = 'Case-classes representing MD5, SHA1 and SHA256.'
                url = 'https://github.com/avast/scala-hashes'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'augi'
                        name = 'Michal August√Ωn'
                        email = 'augustyn.michal@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/avast/scala-hashes.git'
                    developerConnection = 'scm:git:ssh://github.com/avast/scala-hashes.git'
                    url = 'https://github.com/avast/scala-hashes'
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(new String(Base64.decoder.decode(System.getenv('SIGNING_KEY'))), System.getenv('SIGNING_PASSWORD'))
    sign publishing.publications
}

nexusPublishing {
    repositories {
        sonatype()
    }
}
