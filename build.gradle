plugins {
    id 'org.jreleaser' version '1.22.0'
}

allprojects {
    group 'com.avast.hashes'
    version = version == 'unspecified' ? 'DEVELOPER-SNAPSHOT' : version
}

ext {
    circeVersion = '0.14.15'
}

tasks.register('clean') {}

subprojects {
    apply plugin: 'scala'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'org.jreleaser'

    repositories {
        mavenCentral()
    }

    dependencies {
        api "org.scala-lang:scala-library:${scalaLibraryVersion}"

        testImplementation "org.scalatest:scalatest_${scalaVersion}:3.3.0-SNAP4"
        testImplementation "org.scalatestplus:junit-4-12_${scalaVersion}:3.1.3.0"
        testImplementation "junit:junit:4.13.2"
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    afterEvaluate {
        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from(components.java)
                    artifactId = tasks.jar.archiveBaseName.get()
                    pom {
                        name = 'Scala Hashes'
                        description = 'Case-classes representing MD5, SHA1 and SHA256.'
                        url = 'https://github.com/avast/scala-hashes'
                        licenses {
                            license {
                                name = 'The MIT License'
                                url = 'http://www.opensource.org/licenses/mit-license.php'
                            }
                        }
                        developers {
                            developer {
                                id = 'augi'
                                name = 'Michal August√Ωn'
                                email = 'augustyn.michal@gmail.com'
                            }
                        }
                        scm {
                            connection = 'scm:git:git://github.com/avast/scala-hashes.git'
                            developerConnection = 'scm:git:ssh://github.com/avast/scala-hashes.git'
                            url = 'https://github.com/avast/scala-hashes'
                        }
                    }
                }
            }
            repositories {
                maven {
                    url = layout.buildDirectory.dir('staging-deploy')
                }
            }
        }
    }

    jreleaser {
        signing {
            active = 'ALWAYS'
            armored = true
            String base64Key = System.getenv('SIGNING_KEY')
            if (base64Key) {
                secretKey = new String(Base64.decoder.decode(base64Key))
            }
            String base64PublicKey = System.getenv('SIGNING_PUBLIC_KEY')
            if (base64PublicKey) {
                publicKey = new String(Base64.decoder.decode(base64PublicKey))
            }
        }
        deploy {
            maven {
                mavenCentral {
                    sonatype {
                        active = 'ALWAYS'
                        url = 'https://central.sonatype.com/api/v1/publisher'
                        stagingRepository('build/staging-deploy')
                    }
                }
            }
        }
        release {
            github {
                enabled = false
            }
        }
    }
}
