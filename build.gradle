plugins {
    id 'signing'
    id('io.github.gradle-nexus.publish-plugin') version '2.0.0'
}

allprojects {
    group 'com.avast.hashes'
    version = version == 'unspecified' ? 'DEVELOPER-SNAPSHOT' : version
}

ext {
    circeVersion = '0.14.8'
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

subprojects {
    apply plugin: 'scala'
    apply plugin: 'java-library'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    sourceCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()
    }

    dependencies {
        api "org.scala-lang:scala-library:${scalaLibraryVersion}"

        testImplementation "org.scalatest:scalatest_${scalaVersion}:3.3.0-SNAP3"
        testImplementation "org.scalatestplus:junit-4-12_${scalaVersion}:3.1.3.0"
        testImplementation "junit:junit:4.13.2"
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    afterEvaluate {
        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from(components.java)
                    artifactId = archivesBaseName
                    pom {
                        name = 'Scala Hashes'
                        description = 'Case-classes representing MD5, SHA1 and SHA256.'
                        url = 'https://github.com/avast/scala-hashes'
                        licenses {
                            license {
                                name = 'The MIT License'
                                url = 'http://www.opensource.org/licenses/mit-license.php'
                            }
                        }
                        developers {
                            developer {
                                id = 'augi'
                                name = 'Michal August√Ωn'
                                email = 'augustyn.michal@gmail.com'
                            }
                        }
                        scm {
                            connection = 'scm:git:git://github.com/avast/scala-hashes.git'
                            developerConnection = 'scm:git:ssh://github.com/avast/scala-hashes.git'
                            url = 'https://github.com/avast/scala-hashes'
                        }
                    }
                }
            }
        }
    }

    signing {
        String base64Key = System.getenv('SIGNING_KEY')
        if (base64Key) {
            useInMemoryPgpKeys(new String(Base64.decoder.decode(base64Key)), System.getenv('SIGNING_PASSWORD'))
            sign publishing.publications
        }
    }
}
